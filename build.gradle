plugins {
    id 'java'
    id 'org.springframework.boot' version '3.0.6' apply false
    id 'io.spring.dependency-management' version '1.1.0' apply false
    id 'jacoco'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'jacoco'

    jacoco {
        toolVersion = "$jacocoVersion"
    }

    group = 'kr.co.yapp.22nd.coffice'
    version = '0.0.1-SNAPSHOT'
    sourceCompatibility = '17'

    repositories {
        mavenCentral()
    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    dependencies {
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
    }

    tasks.named('jar') {
        enabled = false
    }

    tasks.named('test') {
        useJUnitPlatform()
        finalizedBy jacocoTestReport
    }

    tasks.named('jacocoTestReport') {
        dependsOn test
        reports {
            xml.enabled true
            csv.enabled false
            html.enabled false
        }
    }
}
